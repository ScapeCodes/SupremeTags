plugins {
    id 'java-library'
    id 'maven-publish'
}

sourceCompatibility = 21
targetCompatibility = 21
group = 'net.noscape.project.supremetags'
version = '2.1.13'

java {
    disableAutoTargetJvm()
}

repositories {
    mavenCentral()

    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://repo.codemc.org/repository/maven-snapshots/" }
    maven { url 'https://repo.codemc.io/repository/creatorfromhell/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://repo1.maven.org/maven2/' }
    maven { url 'https://repo.rosewooddev.io/repository/public/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.nexomc.com/releases/' }
    maven { url 'https://repo.nexomc.com/snapshots' }
    maven { url 'https://repo.oraxen.com/releases' }
    maven { url "https://repo.oraxen.com/releases" }
    maven { url "https://repo.nightexpressdev.com/releases" }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven {
        name "essentialsx-releases"
        url "https://repo.essentialsx.net/releases/"
    }
    maven { url "https://repo.alessiodp.com/releases/" }
    maven { url = "https://repo.lushplugins.org/releases/" }
}

configurations.all {
    resolutionStrategy {
        force 'io.papermc.paper:paper-api:1.21.9-R0.1-SNAPSHOT'
        eachDependency {
            if (requested.group == 'org.spigotmc' && requested.name == 'spigot-api') {
                useTarget 'io.papermc.paper:paper-api:1.21.9-R0.1-SNAPSHOT'
                because 'Force resolution to match Paper 1.21.9 API'
            }
        }
    }
}

configurations {
    shade
    compileOnly.extendsFrom(shade)
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'

    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'net.milkbowl.vault:VaultUnlockedAPI:2.16'
    compileOnly 'com.h2database:h2:2.1.214'
    compileOnly 'com.arcaniax:HeadDatabase-API:1.3.3-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'
    compileOnly 'org.mariadb.jdbc:mariadb-java-client:3.3.2'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'org.black_ixx:playerpoints:3.3.2'
    compileOnly 'com.github.LoneDev6:itemsadder-api:3.6.3-beta-14'
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.15.3'
    compileOnly 'io.th0rgal:oraxen:1.190.0'
    compileOnly 'su.nightexpress.coinsengine:CoinsEngine:2.5.0'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.3.0'
    compileOnly 'com.github.retrooper:packetevents-spigot:2.9.3'
    compileOnly 'io.github.valerashimchuck:simpleitemgenerator-api:1.5.0'
    compileOnly 'net.essentialsx:EssentialsX:2.21.2'
    shade "org.lushplugins:ChatColorHandler:4.0.0"

    compileOnly files('libs/Score.jar')
    compileOnly files('libs/ExecutableBlocks.jar')

    shade 'com.github.cryptomorin:XSeries:13.5.1'

    compileOnly 'mysql:mysql-connector-java:8.0.33'
    compileOnly 'redis.clients:jedis:5.1.0'
    compileOnly 'com.zaxxer:HikariCP:5.1.0'
    shade 'net.kyori:adventure-text-minimessage:4.25.0'
    shade 'de.rapha149.signgui:signgui:2.5.4'
    compileOnly "com.nexomc:nexo:1.6.0"
    shade 'commons-lang:commons-lang:2.6'
    shade 'net.wesjd:anvilgui:1.10.5-SNAPSHOT'
    compileOnly 'net.byteflux:libby-bukkit:1.3.1'
}

processResources {
    def props = [
            version: project.version,
            name: project.name
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

def devBuild = project.hasProperty('dev') && project.property('dev').toBoolean()
def devBuildVersion = project.findProperty('devBuildVersion') ?: 'SNAPSHOT'

jar {
    archiveBaseName.set("SupremeTags")

    if (devBuild) {
        archiveVersion.set("${project.version}-DEV-${devBuildVersion}")
    } else {
        archiveVersion.set(project.version.toString())
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Main-Class': 'net.noscape.project.supremetags.SupremeTags',
                'Implementation-Title': 'SupremeTags',
                'Implementation-Version': project.version.toString()
        )
    }

    // Include shaded dependencies
    from {
        configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}